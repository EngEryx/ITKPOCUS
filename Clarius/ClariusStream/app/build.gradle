plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def Properties props = new Properties()
props.load(project.rootProject.file('local.properties').newDataInputStream())

// generates a dummy libpthread.a so that we pass linker errors until deployment
// TODO: make this a proper build dependency so it automatically runs
task generateEmptyPthread(type:Exec) {
    commandLine props['ndk.ar.exe'], 'r', './src/main/cpp/libpthread.a'
}

// downloads and extracts the Clarius mobileapi code
// TODO: make this a custom task with output as a build dependency
task gatherClarius(type:Exec) {
    String osName = System.getProperty("os.name").toLowerCase();
    workingDir '../'
    if (osName.contains("windows")) {
        commandLine 'cmd', '/c', 'Powershell  -File  get_clarius.ps1'
    }
    else {
        commandLine 'sh', './get_clarius.sh'
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.kitware.medical.clariusstream"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        buildConfigField("String", "CLARIUS_PACKAGE_NAME", clariusPackageName)
        buildConfigField("String", "CLARIUS_SERVICE_NAME", clariusServiceName)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // this is for building OpenIGTLink for Android
        externalNativeBuild {
            cmake {
                version "3.18.1"
                arguments "-DON_ANDROID=ON", "-DSWIG_EXECUTABLE=\'" + props['swig.exe'] + "\'"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    externalNativeBuild {
        cmake {
            version "3.18.1"
            path file('CMakeLists.txt')
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation project(':mobileapi-8.0.1:mobileapi') // clarius mobileapi
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

